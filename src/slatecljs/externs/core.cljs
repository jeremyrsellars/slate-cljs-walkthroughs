(ns slatecljs.externs.core
  (:require [clojure.string :as string]
            [clojure.pprint :as pprint]
            [goog.object :as gobj])
  (:require-macros [slatecljs.github :refer [source-bookmark]]))

(def bookmark (source-bookmark "src"))

(defn generated-by-header
  [{:keys [js-var-name ns-name]}]
  (println "// Generated via https://jeremyrsellars.github.io/slate-cljs-walkthroughs/closure_externs.html")
  (println "// from NPM.")
  (println)
  (println "// Reference ns as" ns-name "or use global" (str "js/" js-var-name))
  (println "/**********************************************************************")
  (println " * Extern for global" js-var-name)
  (println " * Generated by https://jeremyrsellars.github.io/slate-cljs-walkthroughs/closure_externs.html")
  (println " **********************************************************************/"))

(def fn-extern (fn [] "function() {}"))

(defn describe-externs
  [x]
  (cond (fn?     x)    fn-extern
        (string? x)    ""
        (array?  x)    #js []
        (number? x)    #js []

        (object? x)
        (into (sorted-map)
          (zipmap
            (gobj/getKeys x)
            (map describe-externs (gobj/getValues x))))

        :default       x))

(def ^:dynamic *indent* 0)
(def indent-str "   ") ; Use a string of 3 spaces for an effective indent of 4 spaces

(defn println-indent
  [& xs]
  (apply println (concat (repeat *indent* indent-str) xs)))

(defn println-extern-kv
  [entries]
  (cond
    (map? entries)
    (doseq [[k v] entries]
      (cond (map? v)
            (do
              (println-indent (str (pr-str (str k)) \:) \{)
              (binding [*indent* (inc *indent*)]
                (println-extern-kv v))
              (println-indent "},"))

            (fn? v)
            (println-indent (str (pr-str (str k)) \:) (str (v) \,))

            :default
            (println-indent (str (pr-str (str k)) \:) (str (js/JSON.stringify v) \,))))
    
    :default
    (js/console.warn entries)))

(defn externs-str
  [{:keys [js-var-name ns-name] :as names} externs]
  (with-out-str
    (generated-by-header names)
    (println-indent "var" js-var-name \= \{)
    (binding [*indent* (inc *indent*)]
      (println-extern-kv externs))
    (println-indent "};")))
